{
 * This Source Code Form is subject to the terms of the Mozilla Public License,
 * v. 2.0. If a copy of the MPL was not distributed with this file, You can
 * obtain one at https://mozilla.org/MPL/2.0/
 *
 * Copyright (C) 2007-2021, Peter Johnson (gravatar.com/delphidabbler).
 *
 * Pascal script for use in [Code] Section of CodeSnip's Install.iss.
 *
 * Helper file for working with Unicode. Requires the Unicode version of Inno
 * Setup.
}


const
  // Constants for Windows CreateFile API function.
  GENERIC_WRITE = $40000000;
  CREATE_ALWAYS = 2;

// Import for Windows CreateFileW API function.
function CreateFileW(
  lpFileName: string; // PWideChar;
  dwDesiredAccess, dwShareMode: LongWord; //DWORD;
  lpSecurityAttributes: LongWord; // PSecurityAttributes;
  dwCreationDisposition, dwFlagsAndAttributes: LongWord; //DWORD;
  hTemplateFile: THandle
): THandle;
external 'CreateFileW@kernel32.dll stdcall';

// Import for Windows WriteFile API function. Prototype specialised for writing
// Unicode strings.
function WriteFile(
  hFile: THandle;
  const Buffer: string; // untyped param
  nNumberOfBytesToWrite: LongWord; // DWORD;
  var lpNumberOfBytesWritten: LongWord; // DWORD;
  lpOverlapped: Cardinal // POverlapped
): BOOL;
external 'WriteFile@kernel32.dll stdcall';

// Import for Windows CloseHandle API function.
function CloseHandle(
  hObject: THandle
): BOOL;
external 'CloseHandle@kernel32.dll stdcall';

// Writes the lines of Unicode strings from the given string array to a named
// file. Each line is terminated with CRLF.
procedure WriteStringsToUnicodeFile(FileName: string; Lines: TArrayOfString);
var
  F: THandle;             // file handle
  I: Integer;             // loops thru string array
  BytesWritten: LongWord; // dummy for use with WriteFile
  Line: string;           // each line from array
  BOM: WideChar;          // stores UTF-16LE byte order mark
begin
  // Create new file
  F := CreateFileW(
    FileName,
    GENERIC_WRITE,
    0,
    0,
    CREATE_ALWAYS,
    FILE_ATTRIBUTE_ARCHIVE,
    0
  );
  if F = LongWord(-1) then
  begin
    MsgBox('Error - can''t create file ' + FileName, mbError, MB_OK);
    Exit;
  end;
  // Write BOM for UTF-16LE
  BOM := #$FEFF;
  WriteFile(
    F,
    BOM,
    2,      // Size of a Unicode char
    BytesWritten,
    0
  );
  // Write each element of string array terminated by CRLF
  for I := 0 to Length(Lines) - 1 do
  begin
    Line := Lines[I] + #13#10;
    WriteFile(
      F,
      Line,
      Length(Line) * 2, // These are 2 byte Unicode chars
      BytesWritten,
      0
    );
  end;
  // Close the file
  CloseHandle(F);
end;

